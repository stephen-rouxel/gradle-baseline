/*
 * Maintained by brightSPARK Labs.
 * www.brightsparklabs.com
 *
 * Refer to LICENSE at repository root for license details.
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.12.0"

    // Apply the Groovy plugin to add support for Groovy
    id "groovy"

    // Apply the Java plugin to add support for Java
    id "java"

    // -------------------------------------------------------------------------
    // PLUGINS THIS PLUGIN WILL APPLY WHEN ADDED
    // -------------------------------------------------------------------------

    // NOTE: KEEP THESE VERSIONS ALIGNED WITH THE DEPENDENCIES BLOCK

    // Apply the Spotless plugin for formatting
    id "com.diffplug.spotless" version "5.8.2"

    // Apply the Error Prone plugin for static code analysis
    id "net.ltgt.errorprone" version "1.3.0"

    // Apply the Gradle Versions + Use Latest Versions plugins for stale dependency checks
    id "com.github.ben-manes.versions" version "0.36.0"
    id "se.patrikerdes.use-latest-versions" version "0.2.15"

    // Apply the OWASP plugin for vulnerability dependency checks
    id "org.owasp.dependencycheck" version "6.0.3"

    // Apply the shadow plugin to build fat jars
    id "com.github.johnrengelman.shadow" version "5.2.0"

    // Apply dependency licence report plugin
    id "com.github.jk1.dependency-license-report" version "1.16"
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation(
            // Add external plugins here so they can be added via our plugin
            // NOTE: KEEP THESE VERSIONS ALIGNED WITH THE PLUGINS BLOCK
            // NOTE: Obtain plugin dependency strings from plugins.gradle.org page
            "com.diffplug.spotless:spotless-plugin-gradle:5.8.2",
            "net.ltgt.errorprone:net.ltgt.errorprone.gradle.plugin:1.3.0",
            "com.google.errorprone:error_prone_core:2.4.0",
            "com.github.ben-manes:gradle-versions-plugin:0.36.0",
            "se.patrikerdes:gradle-use-latest-versions-plugin:0.2.15",
            "org.owasp:dependency-check-gradle:6.0.3",
            "com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:5.2.0",
            "com.github.jk1:gradle-license-report:1.16"
            )

    // Use the awesome Spock testing and specification framework
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"

    //testImplementation 'org.codehaus.groovy:groovy-all:2.4.15'
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description = "Applies brightSPARK Labs standardisation to gradle projects"
project.group       = "com.brightsparklabs"

def versionProcess  = "git describe --always --dirty".execute()
versionProcess.waitFor()
project.version     = versionProcess.exitValue() == 0 ? versionProcess.text.trim() : "0.0.0-UNKNOWN"

project.ext.scm     = "https://github.com/brightsparklabs/gradle-baseline.git"
project.ext.url     = "https://github.com/brightsparklabs/gradle-baseline"

// -----------------------------------------------------------------------------
// java-gradle-plugin
// -----------------------------------------------------------------------------

gradlePlugin {
    // Define the plugin
    plugins {
        baseline {
            id = "com.brightsparklabs.gradle.baseline"
            implementationClass = "com.brightsparklabs.gradle.baseline.BaselinePlugin"
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

pluginBundle {
    website = project.url
    vcsUrl = project.scm
    description = project.description
    tags = [
        "brightsparklabs",
        "gradle",
        "baseline",
    ]

    plugins {
        baseline {
            displayName = "Gradle Baseline at brightSPARK Labs"
        }
    }
}

// -----------------------------------------------------------------------------
// spotless
// -----------------------------------------------------------------------------

def header = """/*
               | * Maintained by brightSPARK Labs.
               | * www.brightsparklabs.com
               | *
               | * Refer to LICENSE at repository root for license details.
               | */
             """.stripMargin("|")
spotless {
    java {
        licenseHeader(header)
        googleJavaFormat().aosp()
    }
    groovy {
        licenseHeader(header)
        excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
        // the Groovy Eclipse formatter extends the Java Eclipse formatter,
        // so it formats Java files by default (unless `excludeJava` is used).
        greclipse()
        indentWithSpaces(4)
    }
    groovyGradle {
        // same as groovy, but for .gradle (defaults to "*.gradle")

        // unfortunately licenseHeader is currently unsupported on groovyGradle
        //licenseHeader(header)

        greclipse()
        indentWithSpaces(4)
    }
}
